plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

private Integer generateVersionCode() {
    return project.minimumSdkVersion * 10000000 + project.versionMajor * 10000 + project.versionMinor * 100 + project.versionPatch
}

private String generateVersionName() {
    return "${project.versionMajor}.${project.versionMinor}.${project.versionPatch}"
}

project.ext {
    versionMajor = 1
    versionMinor = 0
    versionPatch = 0
    minimumSdkVersion = 21
    compileAndTargetSdkVersion = 32
    appName = "xml.about.me"
}

android {
    compileSdkVersion project.compileAndTargetSdkVersion
    defaultConfig {
        applicationId project.appName
        minSdkVersion project.minimumSdkVersion
        targetSdkVersion project.compileAndTargetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", "API_URL", "\"https://newsapi.org/v2/\"")
    }

    buildFeatures {
        dataBinding = true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace project.appName

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Xml-AboutMe-${variant.versionName}-${variant.versionCode}.apk"
        }
    }
}

dependencies {

    // UI
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.appcompat:appcompat:1.4.2"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.google.android.material:material:1.6.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"

    // Architecture Components
    def life_cycle_version = "2.5.0-rc02"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$life_cycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$life_cycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$life_cycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    // Room
    def room_version = "2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$navigation_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$navigation_version"

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Okhttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.3"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    // Moshi Kotlin
    def moshi_version = "1.13.0"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"

    // Glide
    def glide_version = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"

    // Coroutine
    def coroutines_version = "1.6.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // KTX
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.fragment:fragment-ktx:1.4.1"

    // Preferences DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Utils
    api "com.jakewharton.timber:timber:5.0.1"
    implementation "com.github.fondesa:recycler-view-divider:3.5.0"
}

kapt {
    correctErrorTypes = true
}
