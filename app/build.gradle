plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs'
    id 'dagger.hilt.android.plugin'
}

private Integer generateVersionCode() {
    return project.minimumSdkVersion * 10000000 + project.versionMajor * 10000 + project.versionMinor * 100 + project.versionPatch
}

private String generateVersionName() {
    return "${project.versionMajor}.${project.versionMinor}.${project.versionPatch}"
}

project.ext {
    versionMajor = 1
    versionMinor = 0
    versionPatch = 0
    minimumSdkVersion = 21
    appName = "com.baloot.test"
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId project.appName
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 30
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", "API_URL", "\"https://newsapi.org/v2/\"")
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "BalootTest-${variant.versionName}-${variant.versionCode}.apk"
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // UI
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"

    // Architecture Components
    def life_cycle_version = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$life_cycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$life_cycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$life_cycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    // Room
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$navigation_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$navigation_version"

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Okhttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.1"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    // Moshi Kotlin
    def moshi_version = "1.11.0"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    def kotshi_version = "2.4.0"
    implementation "se.ansman.kotshi:api:$kotshi_version"
    kapt "se.ansman.kotshi:compiler:$kotshi_version"

    // Glide
    def glide_version = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"

    // Coroutine
    def coroutines_version = "1.4.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // KTX
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "androidx.fragment:fragment-ktx:1.3.4"

    // Preferences DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0-beta01"

    // Utils
    api "com.jakewharton.timber:timber:4.7.1"
    implementation "com.github.fondesa:recycler-view-divider:3.5.0"
}
